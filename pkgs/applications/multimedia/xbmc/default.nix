{
  stdenv, fetchurl, boost, mesa, glew, mysql, libass, libmpeg2, libmad,
  libjpeg, libsamplerate, libogg, libvorbis, libmodplug, curl, flac, libgcrypt,
  bzip2, libtiff, lzo, yajl, pkgconfig, fontconfig, fribidi, sqlite, libpng,
  pcre, libcdio, freetype, jasper, SDL, SDL_mixer, SDL_image, alsaLib, dbus,
  libbluray, libmicrohttpd, libXmu, samba, udev, libusb, python, cmake, hal,
  gperf, unzip, avahi, zip, nasm, pulseaudio, ftgl,

  # TODO: Check which of these are actually necessary.
  
  autoconf, automake, libtool, gettext
}:

# TODO: Check whether unzip is actually needed.

stdenv.mkDerivation {
  name = "xbmc-11.0";

  src = fetchurl {
    url = http://mirrors.xbmc.org/releases/source/xbmc-11.0.tar.gz;
    sha256 = "068bgg6h593xwwinyqy8wsn4hpz90ib59g0k5dpg4f31q48d7r8z";
  };

  buildInputs = [
    boost mesa glew mysql libass libmpeg2 libmad libjpeg libsamplerate libogg
    libvorbis libmodplug curl flac libgcrypt bzip2 libtiff lzo yajl pkgconfig
    fontconfig fribidi sqlite libpng pcre libcdio freetype jasper SDL SDL_mixer
    SDL_image alsaLib dbus libbluray libmicrohttpd libXmu samba udev libusb
    python cmake hal gperf unzip avahi zip nasm pulseaudio ftgl

    autoconf automake libtool gettext
  ];

  # TODO: Can we leave out some of these options?

  dontFixCmake = true;
  dontUseCmakeBuildDir = true;
  dontUseCmakeConfigure = true;

  # configure performs a SONAME check for some libraries by
  #
  # > $CC -nostdlib -o /dev/null $LDFLAGS -l$2 -Wl,-M 2>/dev/null
  #
  # The $2 is the library name under inspection, e.g. bluray. However, the
  # option -o /dev/null raises an error.  Since this doesn't appear to be
  # necessary, we can simply remove this option. Note that ./configure is
  # generated by ./bootstrap.

  patchPhase = ''
    sed -i 's@CC -nostdlib -o /dev/null@CC -nostdlib@g' configure.in
  '';

  configurePhase = ''
    ./bootstrap
    ./configure --prefix="$out"
  '';
}

